plugins {
    id 'java'
    id 'org.gradle.test-retry' version '1.5.10'
}

group = 'com.automation'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs.addAll(['--release', '21'])
}

ext {
    seleniumVersion = "4.35.0"
    testNGVersion = "7.11.0"
    log4jVersion = "2.25.1"
    extentReportVersion = "5.1.2"
    fakerVersion = "2.3.1"
    restAssuredVersion = "5.5.6"
    commonsIOVersion = "2.20.0"
    johnzonVersion = "1.2.21"
    jsonVersion = "5.5.6"
    jacksonVersion = "2.20.0"
    gsonVersion = "2.13.2"
    yamlVersion = "2.5"
    lombokVersion = "1.18.42"
    dotenvVersion = "3.0.0"
}

dependencies {
    implementation("org.seleniumhq.selenium:selenium-java:${seleniumVersion}")
    implementation("org.testng:testng:${testNGVersion}")
    implementation("org.apache.logging.log4j:log4j-core:${log4jVersion}")
    implementation("org.apache.logging.log4j:log4j-api:${log4jVersion}")
    implementation("com.aventstack:extentreports:${extentReportVersion}")

    implementation("net.datafaker:datafaker:${fakerVersion}")
    implementation("io.rest-assured:rest-assured:${restAssuredVersion}") {
        exclude group: "org.codehaus.jackson"
    }
    implementation("commons-io:commons-io:${commonsIOVersion}")
    implementation("org.apache.johnzon:johnzon-core:${johnzonVersion}")
    implementation("org.apache.johnzon:johnzon-jsonb:${johnzonVersion}")

    implementation("io.rest-assured:json-path:${jsonVersion}")
    implementation("io.rest-assured:json-schema-validator:${jsonVersion}")

    implementation("com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}")
    implementation("com.google.code.gson:gson:${gsonVersion}")

    implementation("org.yaml:snakeyaml:${yamlVersion}")

    compileOnly ("org.projectlombok:lombok:${lombokVersion}") // Use the latest version
    annotationProcessor ("org.projectlombok:lombok:${lombokVersion}")
    testCompileOnly ("org.projectlombok:lombok:${lombokVersion}")
    testAnnotationProcessor ("org.projectlombok:lombok:${lombokVersion}")

    implementation ("io.github.cdimascio:dotenv-java:${dotenvVersion}")
}

tasks.withType(Test) {
    systemProperty 'env', System.properties['env']
    systemProperty 'testSuite', System.properties['testSuite']
    systemProperty 'runOn', System.properties['runOn']
    systemProperty 'platform', System.properties['platform']
    systemProperty 'browser', System.properties['browser']
    systemProperty 'headless', System.properties['headless']

    // Forward credentials to the test JVM (passed via -Dadmin.username etc. from Jenkins)
    systemProperty 'admin.username', System.properties['admin.username']
    systemProperty 'admin.password', System.properties['admin.password']
    systemProperty 'basic.username', System.properties['basic.username']
    systemProperty 'basic.password', System.properties['basic.password']
    systemProperty 'basic.email', System.properties['basic.email']

    useTestNG() {
        useDefaultListeners = false // Disable TestNG HTML reports (using ExtentReports instead)
        outputDirectory = file("$projectDir/test-output") // folder chá»©a test report

        def groups = System.getProperty('groups')
        // If the 'groups' property is provided, include only those groups
        if (groups != null) {
            useTestNG() {
                includeGroups groups.split(',') // Supports multiple comma-separated groups
            }
        }
    }

    // Retry failed tests if enabled via Jenkins parameter (-Dretry.enabled=true)
    retry {
        maxRetries = System.getProperty('retry.enabled', 'false') == 'true' ? 1 : 0
        maxFailures = 20
        failOnPassedAfterRetry = false
    }

    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
    }
}

tasks.register('testSuite', Test) {
    def suite = System.getProperty('suite')

    if (suite != null) {
        def suitePath = "src/test/resources/${suite}.xml"
        useTestNG {
            suites suitePath
        }
    }
}

// JavaDoc generation task - creates HTML documentation from JavaDoc comments
// BEST PRACTICE: Only document framework code (src/main/java) + reusable test helpers
// Excludes:
//   - Test cases (testcases.*) - self-documenting via test names and reports
//
// NOTE: Model classes are included because they're referenced in API method signatures,
// but they are simple data containers (Lombok POJOs) with minimal documentation.
javadoc {
    source = sourceSets.main.allJava

    // Include ONLY reusable test helpers (not test cases)
    source += fileTree(dir: 'src/test/java', include: 'helpers/**/*.java')

    classpath = configurations.compileClasspath + configurations.testCompileClasspath
    destinationDir = file("$projectDir/docs/javadoc")

    options.encoding = 'UTF-8'
    options.charSet = 'UTF-8'
    options.author = true
    options.version = true
    options.windowTitle = 'Selenium Automation Framework - API Documentation'
    options.docTitle = 'Selenium Automation Framework - Core API'
    options.header = 'Test Automation Framework API'
    options.footer = 'Framework Documentation - For internal use'

    // Group packages for better organization
    options.group("Page Objects", "pages*")
    options.group("Base Classes", "base*")
    options.group("Test Helpers", "helpers*")
    options.group("Utilities", "utils*")
    options.group("Configuration", "config*")
    options.group("Reporting", "reports*")
    options.group("Data Models", "model*")
    options.group("API Clients", "api*")
    options.group("Driver Management", "drivers*")
    options.group("Listeners", "listeners*")

    // Suppress warnings for missing tags
    options.addStringOption('Xdoclint:none', '-quiet')
}
