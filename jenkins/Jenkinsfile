pipeline {
    agent any

    triggers {
        // Check GitHub for changes every 2 minutes and trigger build if found
        // pollSCM('H/2 * * * *')
        
        // Scheduled smoke test every Thursday at 8 AM
        parameterizedCron('H 8 * * 4 %SUITE=smoke;GIT_BRANCH=main')
    }

     options {
        timestamps()
        buildDiscarder(logRotator(numToKeepStr: '15', artifactNumToKeepStr: '15'))
        ansiColor('xterm')
    }

    parameters {
        choice(name: 'ENVIRONMENT',
                choices: 'qa\ndev\nstaging\nproduction\nperf',
                description: 'Required - Select environment'
        )
        choice(name: 'SUITE',
                choices: 'none\nsmoke\nregression\ne2e\nuserManagement\nbrowsing\nbooking',
                description: 'Required: Select suite to run'
        )
        string(name: 'GIT_BRANCH',
                defaultValue: 'main',
                description: 'Required: Enter Git Branch name to clone code'
        )
        choice(name: 'RUN_ON',
                choices: 'Local\nGrid\nPerfecto\nAWS',
                description: 'Required: Select where to run'
        )
        choice(name: 'BROWSER_NAME',
                choices: 'Chrome\nFirefox\nEdge\nSafari',
                description: 'Required: Select browser to run'
        )
        choice(name: 'PLATFORM',
                choices: 'Windows 10\nMac OS Tahoe 26.1\nAndroid Google Pixel 4\nApple Iphone 11',
                description: 'Required: Select platform to run'
        )
        string(name: 'TO_RECIPIENT',
                description: 'Optional: Enter email address to receive automation result'
        )
        choice(name: 'PARALLEL_THREAD',
                choices: '1\n2\n3\n4\n5',
                description: 'Select number of threads to run parallel. Default is 1'
        )
        string(name: 'GROUP',
                defaultValue: '',
                description: 'Optional: If suite is not specified, filter groups to run (Example: -Plogin, -Phigh,...)\nIt is possible to combine groups, separated by comma'
        )
        string(name: 'TEST_PLAN_ID',
                defaultValue: '',
                description: 'Optional: Enter test plan ID to mark automation result on TestRail'
        )
        choice(name: 'RETRY_FAILED_TESTS',
                choices: 'No\nYes',
                description: 'Select Yes if you want to retry failed tests'
        )
        choice(name: 'LOG_LEVEL',
                choices: 'INFO\nDEBUG',
                description: 'Select log level to show in console output. The default level is INFO'
        )
    }

    stages {
        stage('Validate Parameters') {
            steps {
                script {
                    if (params.SUITE == 'none' && !params.GROUP?.trim()) {
                        error("❌ Build aborted: You must select a SUITE or specify a GROUP filter. Both cannot be empty.")
                    }
                }
            }
        }

        stage('Setup') {
            steps {
                script {
                    def runLabel = params.SUITE != 'none' ? params.SUITE : "group:${params.GROUP}"
                    currentBuild.displayName = "#${currentBuild.number} ${runLabel} ${params.ENVIRONMENT} ${params.GIT_BRANCH}"
                    echo "Build: #${currentBuild.number} ${runLabel} ${params.GIT_BRANCH}"
                    echo "Job: ${JOB_NAME}"
                    echo "Build started at ${new Date(currentBuild.startTimeInMillis)}"
                }
            }
        }

        stage('Checkout') {
            steps {
                echo "===> Checking out ${params.GIT_BRANCH}"
                git branch: "${params.GIT_BRANCH}", credentialsId: 'git-cred-key', url: 'git@github.com:trangle298/demo01-selenium-java-framework.git'
            }
            post {
                failure {
                    echo "Checkout failed!"
                }
            }
        }

        stage('Test') {
            steps {
                script {
                    // Use withCredentials to securely pass credentials as environment variables
                    withCredentials([
                        string(credentialsId: "${params.ENVIRONMENT.toUpperCase()}_ADMIN_USERNAME", variable: 'ADMIN_USERNAME'),
                        string(credentialsId: "${params.ENVIRONMENT.toUpperCase()}_ADMIN_PASSWORD", variable: 'ADMIN_PASSWORD'),
                    ]) {
                         // Build gradle command
                        def gradleCmd = ".\\gradlew"
                        
                        if (params.SUITE != 'none') {
                             gradleCmd += " testSuite -Dsuite=${params.SUITE}"
                        } else {
                             gradleCmd += " test"
                        }
                        
                        // Add required params
                        gradleCmd += " -Denv=${params.ENVIRONMENT} -Dbrowser=${params.BROWSER_NAME} -DrunOn=${params.RUN_ON} -Dparallel=classes -Dthreadcount=${params.PARALLEL_THREAD} -Dheadless=true"
                        
                        // Add credentials via environment variables (secure)
                        // gradleCmd += " \"-Dadmin.username=%ADMIN_USERNAME%\" \"-Dadmin.password=%ADMIN_PASSWORD%\"" 
                        gradleCmd += " \"-Dadmin.username=${ADMIN_USERNAME}\" \"-Dadmin.password=${ADMIN_PASSWORD}\""

                        // Add log level
                        if (params.LOG_LEVEL == 'DEBUG') {
                            gradleCmd += " --debug"
                        }
                        
                        // Add retry flag if requested
                        if (params.RETRY_FAILED_TESTS == 'Yes') {
                            gradleCmd += " -Dretry.enabled=true"
                        }
                        
                        echo "Running tests on ${params.RUN_ON} for ${params.ENVIRONMENT} environment with ${params.BROWSER_NAME} browser..."

                        bat gradleCmd
                    }
                }
            }
            post {
                always {
                    echo " ===> Publishing test results..."
                    archiveArtifacts artifacts: 'test-output/**', followSymlinks: false
                    publishHTML([allowMissing: false,
                                alwaysLinkToLastBuild: true,
                                icon: '', keepAll: true,
                                reportDir: 'test-output',
                                reportFiles: 'ExtentReport.html',
                                reportName: 'Extent HTML Report',
                                reportTitles: '',
                                useWrapperFileDirectly: true])
                }
            }
        }
    }
    post {
        always {
            echo "===> Build Status: ${currentBuild.currentResult}"
            echo "===> Build Duration: ${currentBuild.durationString}"
            
            script {
                if (params.TO_RECIPIENT?.trim()) {
                    def status = currentBuild.currentResult
                    def emoji = status == 'SUCCESS' ? '✅' : '❌'
                    
                    emailext(
                        to: "${params.TO_RECIPIENT}",
                        subject: "${emoji} Jenkins Build ${status} - ${env.JOB_NAME} #${env.BUILD_NUMBER}",
                        body: """
                            <h2>Build ${status}</h2>
                            <p><strong>Job:</strong> ${env.JOB_NAME}</p>
                            <p><strong>Build Number:</strong> ${env.BUILD_NUMBER}</p>
                            <p><strong>Suite:</strong> ${params.SUITE}</p>
                            <p><strong>Environment:</strong> ${params.ENVIRONMENT}</p>
                            <p><strong>Browser:</strong> ${params.BROWSER_NAME}</p>
                            <p><strong>Branch:</strong> ${params.GIT_BRANCH}</p>
                            <p><strong>Duration:</strong> ${currentBuild.durationString}</p>
                            <p><a href="${env.BUILD_URL}">View Build</a> | <a href="${env.BUILD_URL}Extent_20HTML_20Report">View Report</a></p>
                        """,
                        mimeType: 'text/html'
                    )
                }
            }
        }
    }
}
